; Tests all 16 x 16 bit = 32 bit integer multiplications
; Out: BC = 0, if correct, prefix error length otherwise
	ORG	0x8000
START:	EXX
	PUSH	HL
	EXX

	LD	IX,XVALUE

LOOP:	LD	B,(IX+0)
	LD	C,(IX+2)
	CALL	MUL8
	LD	(EXPECT),HL
	LD	B,(IX+1)
	LD	C,(IX+3)
	CALL	MUL8
	LD	(EXPECT+2),HL
	LD	B,(IX+0)
	LD	C,(IX+3)
	CALL	MUL8
	CALL	ADDMID
	LD	B,(IX+1)
	LD	C,(IX+2)
	CALL	MUL8
	CALL	ADDMID

	LD	HL,PRODUCT
	EXX
	LD	HL,XVALUE
	LD	DE,YVALUE
	LD	B,2
	CALL	BIGMUL
	EXX
	LD	DE,PRODUCT
	LD	B,4
L2:	DEC	HL
	DEC	DE
	LD	A,(DE)
	CP	(HL)
	JR	NZ,ERROR
	DJNZ	L2

	INC	(IX+0)
	JR	NZ,LOOP
	INC	(IX+1)
	JR	NZ,LOOP
	INC	(IX+2)
	JR	NZ,LOOP
	INC	(IX+3)
	JR	NZ,LOOP

	POP	HL
	EXX
	LD	BC,0
	RET
ERROR:	POP	HL
	LD	A,B
	EXX
	LD	C,A
	LD	B,0
	RET

ADDMID:	LD	A,(EXPECT+1)
	ADD	A,L
	LD	(EXPECT+1),A
	LD	A,(EXPECT+2)
	ADC	A,H
	LD	(EXPECT+2),A
	RET	NC
	INC	(IX+7)
	RET

	INCLUDE "bigmul.asm"
	INCLUDE "mul8bit.asm"
XVALUE:	DEFS	2
YVALUE:	DEFS	2
EXPECT:	DEFS	4
PRODUCT:
