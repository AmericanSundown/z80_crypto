; Tests all 16 x 16 bit = 32 bit integer multiplications
; Out: BC = 0, if correct, prefix error length otherwise
	ORG	0x8000
START:	EXX
	PUSH	HL
	EXX

LOOP:	LD	A,(XVALUE)
	LD	B,A
	LD	A,(YVALUE)
	LD	C,A
	CALL	MUL8
	LD	(EXPECT),HL
	LD	A,(XVALUE+1)
	LD	B,A
	LD	A,(YVALUE+1)
	LD	C,A
	CALL	MUL8
	LD	(EXPECT+2),HL
	LD	A,(XVALUE)
	LD	B,A
	LD	A,(YVALUE+1)
	LD	C,A
	CALL	MUL8
	CALL	ADDMID
	LD	A,(XVALUE+1)
	LD	B,A
	LD	A,(YVALUE)
	LD	C,A
	CALL	MUL8
	CALL	ADDMID

	LD	HL,PRODUCT
	EXX
	LD	HL,XVALUE
	LD	DE,YVALUE
	LD	B,2
	CALL	BIGMUL
	EXX
	LD	DE,PRODUCT
	LD	B,4
L2:	DEC	HL
	DEC	DE
	LD	A,(DE)
	CP	(HL)
	JR	NZ,ERROR
	DJNZ	L2

	LD	HL,(XVALUE)
	INC	HL
	LD	(XVALUE),HL
	LD	A,L
	OR	H
	JR	NZ,LOOP

	LD	HL,(YVALUE)
	INC	HL
	LD	(YVALUE),HL
	LD	A,L
	OR	H
	JR	NZ,LOOP


	POP	HL
	EXX
	LD	BC,0
	RET
ERROR:	POP	HL
	LD	A,B
	EXX
	LD	C,A
	LD	B,0
	RET

ADDMID:	LD	A,(EXPECT+1)
	ADD	A,L
	LD	(EXPECT+1),A
	LD	A,(EXPECT+2)
	ADC	A,H
	LD	(EXPECT+2),A
	RET	NC
	LD	A,(EXPECT+3)
	INC	A
	LD	(EXPECT+3),A
	RET

	INCLUDE "bigmul.asm"
	INCLUDE "mul8bit.asm"
	INCLUDE "multab.asm"
XVALUE:	DEFS	2
	INCLUDE	"align.asm"
YVALUE:	DEFS	2
	INCLUDE	"align.asm"
EXPECT:	DEFS	4
PRODUCT:
