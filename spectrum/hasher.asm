MAKE_ROOM:	EQU	0x1655
STR_DATA1:	EQU	0x1727
SCANNING:	EQU	0x24FB
STK_STO:	EQU	0x2AB2
STK_FETCH:	EQU	0x2BF1
STK_PNTRS:	EQU	0x35BF
CH_TAB:		EQU	0x5C34
ERR_NR:		EQU	0x5C3A
TV_FLAG:	EQU	0x5C3C
CHANS:		EQU	0x5C4F
PROG:		EQU	0x5C53
CH_ADD:		EQU	0x5C5D
FLAGS2:		EQU	0x5C6A

	ORG	63488	; 0xF800
INIT:	LD	HL,(CH_TAB)
	LD	A,H
	OR	L
	RET	NZ	; Channel already open
	LD	HL,(PROG)
	DEC	HL
 	LD	BC,STREAME-STREAM
	PUSH	BC
	CALL	MAKE_ROOM
	LD	HL,STREAM+10
	POP	BC
	LDDR
	EX	DE,HL
	LD	DE,(CHANS)
	AND	A
	SBC	HL,DE
	INC	HL
	INC	HL
	LD	(CH_TAB),HL
INIT2:	RES	6,(IY+FLAGS2-ERR_NR)
INIT3:	RES	5,(IY+FLAGS2-ERR_NR)
	JR	KECCAKI
WRITE:	BIT	5,(IY+FLAGS2-ERR_NR)
	CALL	NZ,INIT3
	EXX
	PUSH	BC
	PUSH	DE
	CALL	KECCAKU
	POP	DE
	POP	BC
	EXX
	RET
READ:	BIT	5,(IY+FLAGS2-ERR_NR)
	CALL	Z,KECCAK
	SET	5,(IY+FLAGS2-ERR_NR)
	BIT	3,(IY+TV_FLAG-ERR_NR)
	JR	NZ,INPUT
	CALL	KECCAKR
	SCF
	RET
INPUT:	LD	HL,6
	ADD	HL,SP
	LD	(HL),0x48	; Work around BEEPER bug in INPUT
	LD	HL,(KECCAKP)
	LD	A,L
	CP	KECCAKS - 0x100 * (KECCAKS / 0x100) + 0x20
	JR	NC,INPUTE
	LD	A,(FLAGS2)
	XOR	0x40
	LD	(FLAGS2),A
	AND	0x40
	LD	A,(HL)
	RRCA
	RRCA
	RRCA
	RRCA
	CALL	Z,KECCAKR
HEXD:	AND	0xF
	ADD	A,0x90
	DAA
	ADC	A,0x40
	DAA
	SCF
	RET
INPUTE:	CALL	INIT2
	RES	3,(IY+TV_FLAG-ERR_NR)	; Work around another bug in INPUT
	LD	A,0xD
	SCF
	RET

	INCLUDE	"../keccak.asm"
STREAM:	DEFW	WRITE
	DEFW	READ
	DEFB	"H"
	DEFW	WRITE
	DEFW	READ
	DEFW	STREAME- STREAM
STREAME:EQU	$
	INCLUDE	"../keccaktab.asm"
KECCAKB:EQU	IOTAT+0x100
KECCAKS:EQU	KECCAKB+48
KECCAKP:EQU	KECCAKS+200

