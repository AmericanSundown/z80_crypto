KECCAKF:LD	B,24
KECCAKL:PUSH	BC
; Tetha
	LD	HL,KECCAKB
	LD	IX,KECCAKS+100
	LD	B,40
TETHAL1:LD	A,(IX-100)
	XOR	(IX-60)
	XOR	(IX-20)
	XOR	(IX+20)
	XOR	(IX+60)
	LD	(HL),A
	INC	L
	DEFB	0xDD
	INC	L	; INC IXL
	DJNZ	TETHAL1
	LD	D,H
	LD	BC,0x5FF
TETHAL2:LD	A,B
	CP	5
	JR	NZ,TETHAM
	XOR	A
TETHAM:	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	L,A
	LD	E,200
	LDI
	LDI
	LDI
	LDI
	LDI
	LDI
	LDI
	LDI
	LD	L,E
	DEC	L
	SLA	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	JR	NC,TETHAR
	LD	L,207
	SET	0,(HL)
TETHAR:	LD	A,B
	DEC	A
	DEC	A
	JP	P,TETHAP
	LD	A,4
TETHAP:	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	E,A
	LD	L,200
	LD	C,B
	LD	B,8
THETALX:LD	A,(DE)
	XOR	(HL)
	LD	(HL),A
	INC	L
	INC	H
	DJNZ	THETALX
	LD	B,8
THETAL3:DEFB	0xDD
	DEC	L	; DEC IXL
	DEC	L
	LD	E,(HL)
	LD	A,(IX-100)
	XOR	E
	LD	(IX-100),A
	LD	A,(IX-60)
	XOR	E
	LD	(IX-60),A
	LD	A,(IX-20)
	XOR	E
	LD	(IX-20),A
	LD	A,(IX+20)
	XOR	E
	LD	(IX+20),A
	LD	A,(IX+60)
	XOR	E
	LD	(IX+60),A
	DJNZ	THETAL3
; Rho
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x08
	LD	BC,7
	LD	DE,-7
	CALL	ROTL1	; 1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x10
	CALL	ROTL4	; 3 = 4 - 1
	CALL	ROTR1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x18
	CALL	ROTL4	; 6 = 4 + 1 + 1
	CALL	ROTL1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x20
	CALL	ROTL8	; 10 = 8 + 1 + 1
	CALL	ROTL1
	CALL	ROTL1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x28
	CALL	ROTL16	; 15 = 16 - 1
	CALL	ROTR1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x30
	CALL	ROTL16	; 21 = 16 + 4 + 1
	CALL	ROTL4
	CALL	ROTL1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x38
	CALL	ROTL24	; 28 = 24 + 4
	CALL	ROTL4
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x40
	CALL	ROT32	; 36 = 32 + 4
	CALL	ROTL4
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x48
	CALL	ROTR16	; 45 = 64 - 16 - 4 + 1
	CALL	ROTR4
	CALL	ROTL1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x50
	CALL	ROTR8	; 55 = 64 - 8 - 1
	CALL	ROTR1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x58
	CALL	ROTL1	; 2 = 1 + 1
	CALL	ROTL1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x60
	CALL	ROTL16	; 14 = 16 - 1 - 1
	CALL	ROTR1
	CALL	ROTR1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x68
	CALL	ROTL24	; 27 = 24 + 4 - 1
	CALL	ROTL4
	CALL	ROTR1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x70
	CALL	ROTR24	; 41 = 64 - 24 + 1
	CALL	ROTL1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x78
	CALL	ROTR8	; 56 = 64 - 8
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x80
	CALL	ROTL8	; 8
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x88
	CALL	ROTL24	; 25 = 24 + 1
	CALL	ROTL1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x90
	CALL	ROTR16	; 43 = 64 - 16 - 4 - 1
	CALL	ROTR4
	CALL	ROTR1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0x98
	CALL	ROTR1	; 62 = 64 - 1 - 1
	CALL	ROTR1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0xA0
	CALL	ROTL16	; 18 = 16 + 1 + 1
	CALL	ROTL1
	CALL	ROTL1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0xA8
	CALL	ROTR24	; 39 = 64 - 24 - 1
	CALL	ROTR1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0xB0
	CALL	ROTR4	; 61 = 64 - 4 + 1
	CALL	ROTL1
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0xB8
	CALL	ROTL16	; 20 = 16 + 4
	CALL	ROTL4
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100) + 0xC0
	CALL	ROTR16	; 44 = 64 - 16 - 4
	CALL	ROTR4
; Pi
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100)
	LD	BC,0x0807
PIL1:	PUSH	BC
	LD	E,KECCAKB - 0x100 * (KECCAKB/0x100)
	LD	B,0x18
PIL2:	LDI
	LD	A,L
	ADD	A,C
	LD	L,A
	DJNZ	PIL2
	LD	E,KECCAKB - 0x100 * (KECCAKB/0x100)
	LD	A,(DE)
	INC	E
	LD	(IX-20),A
	LD	A,(DE)
	INC	E
	LD	(IX-44),A
	LD	A,(DE)
	INC	E
	LD	(IX-12),A
	LD	A,(DE)
	INC	E
	LD	(IX+36),A
	LD	A,(DE)
	INC	E
	LD	(IX+44),A
	LD	A,(DE)
	INC	E
	LD	(IX-76),A
	LD	A,(DE)
	INC	E
	LD	(IX-60),A
	LD	A,(DE)
	INC	E
	LD	(IX+28),A
	LD	A,(DE)
	INC	E
	LD	(IX-36),A
	LD	A,(DE)
	INC	E
	LD	(IX+68),A
	LD	A,(DE)
	INC	E
	LD	(IX+92),A
	LD	A,(DE)
	INC	E
	LD	(IX-68),A
	LD	A,(DE)
	INC	E
	LD	(IX+20),A
	LD	A,(DE)
	INC	E
	LD	(IX+84),A
	LD	A,(DE)
	INC	E
	LD	(IX+52),A
	LD	A,(DE)
	INC	E
	LD	(IX+4),A
	LD	A,(DE)
	INC	E
	LD	(IX-4),A
	LD	A,(DE)
	INC	E
	LD	(IX-84),A
	LD	A,(DE)
	INC	E
	LD	(IX+60),A
	LD	A,(DE)
	INC	E
	LD	(IX+12),A
	LD	A,(DE)
	INC	E
	LD	(IX+76),A
	LD	A,(DE)
	INC	E
	LD	(IX-28),A
	LD	A,(DE)
	INC	E
	LD	(IX-52),A
	LD	A,(DE)
	INC	E
	LD	(IX-92),A
	DEFB	0xDD
	INC	L	; INC IXL
	LD	A,L
	SUB	A,0xBF
	LD	L,A
	POP	BC
	DEC	B
	JP	NZ,PIL1
; Chi
	LD	L,KECCAKS - 0x100 * (KECCAKS/0x100)
	LD	C,5
CHIL0:	PUSH	BC
	LD	E,KECCAKB - 0x100 * (KECCAKB/0x100)
	LD	BC,40
	LDIR
	LD	IX,KECCAKB
	LD	B,24
CHIL1:	LD	A,(IX + 0x08)
	CPL
	AND	(IX + 0x10)
	XOR	(HL)
	LD	(HL),A
	INC	L
	DEFB	0xDD
	INC	L	; INC IXL
	DJNZ	CHIL1
	LD	B,8
CHIL2:	LD	A,(IX + 0x08)
	CPL
	AND	(IX - 0x18)
	XOR	(HL)
	LD	(HL),A
	INC	L
	DEFB	0xDD
	INC	L	; INC IXL
	DJNZ	CHIL2
	LD	B,8
CHIL3:	LD	A,(IX - 0x20)
	CPL
	AND	(IX - 0x18)
	XOR	(HL)
	LD	(HL),A
	INC	L
	DEFB	0xDD
	INC	L	; INC IXL
	DJNZ	CHIL3
	POP	BC
	DJNZ	CHIL0
; Iota
	LD	D,KECCAKS / 0x100
	POP	BC
	LD	A,B
	ADD	A,A
	ADD	A,A
	LD	L,A	; E = B * 4
	LD	H,IOTAT / 0x100
	LD	E,KECCAKS - 0x100 * (KECCAKS / 0x100) + 0
	LD	A,(DE)
	XOR	(HL)
	LD	(DE),A
	INC	L
	LD	E,KECCAKS - 0x100 * (KECCAKS / 0x100) + 4
	LD	A,(DE)
	XOR	(HL)
	LD	(DE),A
	INC	L
	LD	E,KECCAKS - 0x100 * (KECCAKS / 0x100) + 6
	LD	A,(DE)
	XOR	(HL)
	LD	(DE),A
	INC	L
	LD	E,KECCAKS - 0x100 * (KECCAKS / 0x100) + 7
	LD	A,(DE)
	XOR	(HL)
	LD	(DE),A
	DEC	B
	JP	NZ,KECCAKL
	RET

; Cyclic 64 bit rotation helper functions
; In: HL base pointer, BC=7, DE=-7
; Pollutes: AF, AF'

ROTR1:	SRL	(HL)
	INC	L
	RR	(HL)
	INC	L
	RR	(HL)
	INC	L
	RR	(HL)
	INC	L
	RR	(HL)
	INC	L
	RR	(HL)
	INC	L
	RR	(HL)
	INC	L
	RR	(HL)
	ADD	HL,DE
	RET	NC
	SET	7,(HL)
	RET

ROTL1:	LD	A,(HL)
	ADD	A,A
	ADD	HL,BC
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	DEC	L
	RL	(HL)
	RET

ROTR4:	XOR	A
	RRD
	INC	L
	RRD
	INC	L
	RRD
	INC	L
	RRD
	INC	L
	RRD
	INC	L
	RRD
	INC	L
	RRD
	INC	L
	RRD
	ADD	HL,DE
	RRCA
	RRCA
	RRCA
	RRCA
	OR	(HL)
	LD	(HL),A
	RET

ROTL4:	LD	A,(HL)
	RLCA
	RLCA
	RLCA
	RLCA
	ADD	HL,BC
	RLD
	DEC	L
	RLD
	DEC	L
	RLD
	DEC	L
	RLD
	DEC	L
	RLD
	DEC	L
	RLD
	DEC	L
	RLD
	DEC	L
	RLD
	RET

ROTR8:	ADD	HL,BC
	LD	E,L
	LD	D,H
	LD	A,(HL)
	DEC	L
	LDD	; Faster than LDDR
	LDD
	LDD
	LDD
	LDD
	LDD
	LDD
	INC	L
	LD	(HL),A
	LD	DE,-7
	LD	C,7
	RET

ROTL8:	LD	A,(HL)
	LD	E,L
	LD	D,H
	INC	L
	LDI	; Faster than LDIR
	LDI
	LDI
	LDI
	LDI
	LDI
	LDI
	LD	DE,-7
	LD	C,7
	ADD	HL,DE
	DEC	L
	RET

ROTR16:	ADD	HL,BC
	LD	E,L
	LD	D,H
	LD	A,(HL)
	DEC	L
	LD	B,(HL)
	DEC	L
	LDD
	LDD
	LDD
	LDD
	LDD
	LDD
	LD	L,E
	LD	(HL),A
	DEC	L
	LD	(HL),B
	LD	DE,-7
	LD	BC,7
	RET

ROTL16:	LD	A,(HL)
	LD	E,L
	LD	D,H
	INC	L
	LD	B,(HL)
	INC	L
	LDI
	LDI
	LDI
	LDI
	LDI
	LDI
	LD	L,E
	LD	(HL),A
	INC	L
	LD	(HL),B
	LD	DE,-7
	LD	BC,7
	ADD	HL,DE
	DEC	L
	RET

ROTR24:	ADD	HL,BC
	LD	E,L
	LD	D,H
	LD	A,(HL)
	DEC	L
	EX	AF,AF'
	LD	A,(HL)
	DEC	L
	LD	B,(HL)
	DEC	L
	LDD
	LDD
	LDD
	LDD
	LDD
	LD	L,E
	EX	AF,AF'
	LD	(HL),A
	DEC	L
	EX	AF,AF'
	LD	(HL),A
	DEC	L
	LD	(HL),B
	LD	DE,-7
	LD	BC,7
	RET

ROTL24: LD	A,(HL)
	LD	E,L
	LD	D,H
	INC	L
	EX	AF,AF'
	LD	A,(HL)
	INC	L
	LD	B,(HL)
	INC	L
	LDI
	LDI
	LDI
	LDI
	LDI
	LD	L,E
	EX	AF,AF'
	LD	(HL),A
	INC	L
	EX	AF,AF'
	LD	(HL),A
	INC	L
	LD	(HL),B
	LD	DE,-7
	LD	BC,7
	ADD	HL,DE
	DEC	L
	RET

ROT32:	LD	A,L
	ADD	4
	LD	E,A
	LD	D,H
	LD	A,(DE)
	LDI
	DEC	L
	LD	(HL),A
	INC	L
	LD	A,(DE)
	LDI
	DEC	L
	LD	(HL),A
	INC	L
	LD	A,(DE)
	LDI
	DEC	L
	LD	(HL),A
	INC	L
	LD	A,(DE)
	LDI
	DEC	L
	LD	(HL),A
	LD	L,E
	DEC	L
	LD	DE,-7
	LD	C,7
	RET
